iomap   equ $bf00       ;IO page (A15:8)
spi_cmd equ iomap+$f6   ;spi command register, write only
spi_lrd equ iomap+$f6   ;spi last read data register, read only
spi_dat equ iomap+$90   ;spi data register set, read/write
; SPI device list
spi_sd  equ spi_dat     ;SD card slot
spi_165 equ spi_dat+1   ;74HC165 parallel input shift register
spi_595 equ spi_dat+2   ;74HC595 parallel output shift register
spi_eep equ spi_dat+3   ;25LC512 EEPROM

        bss
        org 0
seed        ds  1
modifier    ds  1
        org $20
pattern     ds  16
            ds  3       ;diffusor space
        org $40
read        ds  16

        code        
        org $1000

start
        lda #0
        sta seed
        sta modifier
        lda #%00010000      ;msb 1st, CPOL=0, CPHA=0, 8MHz
        sta spi_cmd
        
inc_test                ;generate test pattern
        ldx #2
        lda seed
inc_test_pre                 
        clc
        adc modifier
        dex
        bpl inc_test_pre
        ldx #15
inc_test_pat
        sta pattern,x
        clc
        adc modifier
        dex
        bpl inc_test_pat
        jsr verify
        inc modifier
        bne inc_test
        inc seed
        bne inc_test

inc_xor                     ;generate pattern with inversion
        ldx #2
        lda seed
inc_xor_pre                 
        clc
        adc modifier
        dex
        bpl inc_xor_pre
        ldx #15
inc_xor_pat
        sta pattern,x
        clc
        adc modifier
        dex
        eor #$ff
        sta pattern,x
        eor #$ff
        clc
        adc modifier
        dex
        bpl inc_xor_pat

        jsr verify
        inc modifier
        bne inc_xor
        inc seed
        bne inc_xor

        jmp start        

verify  ldx #15             ;sub write - read - verify
wr_loop lda pattern,x
        sta spi_595         ;write SIPO
        lda spi_165         ;read back PISO
        sta read,x
        dex
        bpl wr_loop

        ldx #15             ;compare read to lastpat
cp_loop lda pattern,x
        cmp read,x
        bne *               ;data mismatch
        dex
        bpl cp_loop
        rts        
        
        end start
        